buildscript {
    ext.kotlinVersion = '1.1.1'
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    ext {
        appName = "Mundus"
        gdxVersion = '1.9.6'
        aiVersion = '1.8.0'
        visuiVersion = '1.3.0'
        kryoVersion = '4.0.0'
        junitVersion = '4.12'
        commonsIoVersion = '2.5'
        commonsLangVersion = '3.4'

        ktxVersion = '1.9.6-b1'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":commons") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"

        testCompile "junit:junit:$junitVersion"
    }
}

project(":editor") {
    apply plugin: "java"
    apply plugin: "kotlin"

    dependencies {
        compile project(":commons")

        // Kotlin libs
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        // compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
        compile "com.github.czyzby:ktx-inject:$ktxVersion"

        // libGDX
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        // gdx-freetype
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

        // commons
        compile "org.apache.commons:commons-lang3:$commonsLangVersion"
        compile "commons-io:commons-io:$commonsIoVersion"

        // other
        compile "com.kotcrab.vis:vis-ui:$visuiVersion"
        compile "com.esotericsoftware:kryo:$kryoVersion"

        testCompile "junit:junit:$junitVersion"
    }
}

project(":gdx-runtime") {
    apply plugin: "java"

    dependencies {
        compile project(":commons")
    }
}

tasks.eclipse.doLast {
    delete ".project"
}
